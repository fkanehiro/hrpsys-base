/**
 * @file RemoveForceSensorLinkOffsetService.idl
 * @brief Services for removing link offset of force sensors
 */
module OpenHRP
{
  interface RemoveForceSensorLinkOffsetService
  {
    /**
     * @struct forcemomentOffsetParam
     * @brief Parameters for a link mass and center of mass. The link is assumed to be end-effector-side from force-moment sensor.
     */
    typedef sequence<double, 3> DblSequence3;
    typedef sequence<string> StrSequence;
    struct forcemomentOffsetParam {
      /// Force offset [N].
      DblSequence3 force_offset;
      /// Moment offset [Nm].
      DblSequence3 moment_offset;
      /// Link center of mass in sensor frame [m].
      DblSequence3 link_offset_centroid;
      /// Link msas [kg].
      double link_offset_mass;
    };

    /**
     * @brief set offset parameters.
     * @param i_param new offset parameters
     * @return true if set successfully, false otherwise
     */
    boolean setForceMomentOffsetParam(in string name, in forcemomentOffsetParam i_param);

    /**
     * @brief get offset parameters.
     * @param name is name of the offset parameter set and i_param output offset parameters
     * @return true if set successfully, false otherwise
     */
    boolean getForceMomentOffsetParam(in string name, out forcemomentOffsetParam i_param);

    /**
     * @brief loadForceMomentOffsetParams from file
     * @param filename is full-path input file name.
     * @return true if load successfully, false otherwise
     */
    boolean loadForceMomentOffsetParams(in string filename);

    /**
     * @brief dumpForceMomentOffsetParams from file
     * @param filename is full-path output file name.
     * @return true if dump successfully, false otherwise
     */
    boolean dumpForceMomentOffsetParams(in string filename);

    /**
     * @brief remove offsets on sensor outputs form force/torque sensors. Sensor offsets (force_offset and moment_offset in ForceMomentOffsetParam) are calibrated. This function takes several time (for example 8.0[s]). Please keep the robot static and make sure that robot's sensors do not contact with any objects.
     * @param names is list of sensor names to be calibrated. If not specified, all sensors are calibrated by default.
     * @param tm is calibration time[s].
     * @return true if set successfully, false otherwise
     */
    boolean removeForceSensorOffset(in StrSequence names, in double tm);
  };
};
